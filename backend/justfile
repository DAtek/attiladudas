pgks := "\
./api/components/auth \
./api/components/five_in_a_row \
./api/components/gallery \
./api/components/room_manager \
./api/components/user \
./api/handlers/file_rank_patch \
./api/handlers/files_delete \
./api/handlers/files_post \
./api/handlers/galleries_get \
./api/handlers/gallery_delete \
./api/handlers/gallery_get \
./api/handlers/gallery_post \
./api/handlers/gallery_put \
./api/handlers/resize_get \
./api/handlers/token_post \
./api/handlers/ws_five_in_a_row
"
coverfile := ".coverage"


run-integration-services *args:
    docker compose up {{ args }}


migrate *args:
    go run ./cmd/migrate/ {{ args }}


generate-keypair output_dir:
    openssl genpkey -algorithm ed25519 -out {{ output_dir }}/private.pem
    openssl pkey -in {{ output_dir }}/private.pem -pubout -out {{ output_dir }}/public.pem


test *args:
    gotestsum -f dots-v2 -- {{ args }} {{ pgks }}


test-cover *args:
    gotestsum -f dots-v2 -- {{ args }} -coverprofile .coverage.tmp {{ pgks }}
    cat .coverage.tmp | grep -v "mocks.go" > {{ coverfile }}
    rm -f .coverage.tmp


show-coverage:
    go tool cover -html {{ coverfile }}


create-user *args: 
    go run ./cmd/create-user {{ args }}


tidy:
    #!/bin/bash

    rm -f go.work.sum

    for module in api fiber-tools db cmd
    do
        just tidy-module ${module} &
    done

    while true; do
        wait -n
        [ "$?" = "127" ] && break
    done

    go work sync


tidy-module module:
    #!/bin/bash
    echo "Tidying {{ module }}"
    cd {{ module }}
    go mod tidy


