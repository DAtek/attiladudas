pgks := "\
./api/components/auth \
./api/components/five_in_a_row \
./api/components/gallery \
./api/components/room_manager \
./api/handlers/file_rank_patch \
./api/handlers/gallery_get \
./api/handlers/token_post \
./api/handlers/ws_five_in_a_row
"
coverfile := ".coverage"


run-integration-services *args:
    docker compose up {{ args }}


migrate *args:
    go run ./cmd/migrate/ {{ args }}


test *args:
    gotestsum -f dots-v2 -- {{ args }} {{ pgks }}


test-cover *args:
    gotestsum -f dots-v2 -- {{ args }} -coverprofile {{ coverfile }} {{ pgks }}


show-coverage:
    go tool cover -html {{ coverfile }}


tidy:
    #!/bin/bash

    rm -f go.work.sum

    for module in api fiber-tools db cmd
    do
        just tidy-module ${module} &
    done

    while true; do
        wait -n
        [ "$?" = "127" ] && break
    done

    go work sync


tidy-module module:
    #!/bin/bash
    echo "Tidying {{ module }}"
    cd {{ module }}
    go mod tidy


